/*
 * This file is part of Toaster
 *
 * Copyright (c) 2014, 2017 Peter Siegmund
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'

android {
    def versionMajor = 0
    def versionMinor = 5
    def versionPatch = 0
    def versionBuild = 0

    buildToolsVersion '26.0.1'
    compileSdkVersion 26

    signingConfigs {
        mars3142 {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    defaultConfig {
        applicationId "org.mars3142.android.toaster"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix ".debug"
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.mars3142
            }
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.mars3142
            }
        }
    }
}

def supportVersion = "26.0.2"
def componentsVersion = "1.0.0-alpha9"
def glideVersion = "4.1.1"

dependencies {
    compile project(':apprater')

    /* support libraries */
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:palette-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"

    /* architecture components */
    compile "android.arch.lifecycle:runtime:$componentsVersion"
    compile "android.arch.lifecycle:extensions:$componentsVersion"
    compile "android.arch.persistence.room:runtime:$componentsVersion"
    compile "android.arch.persistence.room:rxjava2:$componentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$componentsVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$componentsVersion"

    /* glide */
    compile "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    /* rx */
    compile "io.reactivex.rxjava2:rxjava:2.0.8"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"

    /* timber */
    compile "com.jakewharton.timber:timber:4.5.1"

    /* card library */
    compile "com.github.gabrielemariotti.cards:cardslib-core:2.1.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

if (project.hasProperty("PLAY_STORE_ACCOUNT")) {
    play {
        track = "alpha"
        serviceAccountEmail = PLAY_STORE_ACCOUNT
        pk12File = file(PLAY_STORE_P12)
    }
}
repositories {
    mavenCentral()
}