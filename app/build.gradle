/*
 * This file is part of Toaster
 *
 * Copyright (c) 2015 Peter Siegmund
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    def versionMajor = 0
    def versionMinor = 5
    def versionPatch = 0
    def versionBuild = 0

    buildToolsVersion "23.0.1"
    compileSdkVersion 23

    signingConfigs {
        mars3142 {
            File propsFile = file('keystore.properties')
            if (propsFile.exists()) {
                Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(propsFile))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            } else {
                keyAlias ""
                storePassword ""
                keyPassword ""
            }
        }
    }

    defaultConfig {
        applicationId "org.mars3142.android.toaster"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        useJack false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix ".debug"
            signingConfig signingConfigs.mars3142
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.mars3142
        }
    }
}

dependencies {
    compile project(':apprater')

    /* support libraries */
    compile "com.android.support:appcompat-v7:23.0.1"
    compile "com.android.support:cardview-v7:23.0.1"
    compile "com.android.support:palette-v7:23.0.1"
    compile "com.android.support:recyclerview-v7:23.0.1"

    /* card library */
    compile "com.github.gabrielemariotti.cards:cardslib-core:2.0.1"
}
